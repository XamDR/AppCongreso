plugins {
	id 'com.android.application'
	id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
}

def versionMajor = 2
def versionMinor = 0
def versionPatch = 0

def readKeystoreProperties() {
    def propsFile = rootProject.file('keystore.properties')
    def configName = 'release'
    if (propsFile.exists() && android.signingConfigs.hasProperty(configName)) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        android.signingConfigs[configName].storeFile = file(props['storeFile'])
        android.signingConfigs[configName].storePassword = props['storePassword']
        android.signingConfigs[configName].keyAlias = props['keyAlias']
        android.signingConfigs[configName].keyPassword = props['keyPassword']
    }
}

android {
	signingConfigs {
        release {
            readKeystoreProperties()
        }
    }
    compileSdkVersion 31
	buildToolsVersion "30.0.3"

	defaultConfig {
		applicationId "edu.icontinental.congresoi40"
		minSdkVersion 23
		targetSdkVersion 31
		versionCode 2
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

	buildTypes {
        debug {
            debuggable true
            versionNameSuffix ".debug"
            resValue "string", "app_version",
                    "${defaultConfig.versionName}${versionNameSuffix}"
        }
		release {
            debuggable false
			minifyEnabled true
            shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_version",
                    "${defaultConfig.versionName}"
            signingConfig signingConfigs.release
		}
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
	}
	kotlinOptions {
		jvmTarget = '1.8'
	}
    buildFeatures {
        viewBinding true
    }
}

dependencies {
	implementation 'androidx.core:core-ktx:1.7.0'
	implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.5.0-beta01'
	implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
	implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
	implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.0'
	implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.0'
	implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    // Import the BoM for the Firebase platform
    implementation platform('com.google.firebase:firebase-bom:27.1.0')
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-firestore-ktx'
    implementation 'com.google.firebase:firebase-storage-ktx'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.1.1'

    // Also declare the dependency for the Google Play services library and specify its version
    implementation 'com.google.android.gms:play-services-auth:19.2.0'

    // Add coil to load images from Firebase
    implementation("io.coil-kt:coil:1.2.1")

    // PinView
    implementation 'io.github.chaosleung:pinview:1.4.4'

    // Java Mail
    implementation files('../libs/activation.jar')
    implementation files('../libs/additionnal.jar')
    implementation files('../libs/mail.jar')

    // Splash Screen
    implementation 'androidx.core:core-splashscreen:1.0.0-alpha02'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}